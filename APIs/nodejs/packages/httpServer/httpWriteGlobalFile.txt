#{
	Serves a file (like $writeFile propOf _request), but with additional support for the "ETag" header and HTTP 304 (not modified) responses.
	The function can automatically write the file type to the request, which prevents other headers from being accepted later. So all headers should be set before calling this function.
	
	It is assumed that a file exists at the given path.
	The served file is allowed to change later - because of that, the ETag is generated every time the file is accessed.
	
	Usage example:
		_request httpWriteGlobalFile "path/to/a/file".
	
	If you want to serve files which do not change later, consider using "httpWriteStaticFile".
}
$httpWriteGlobalFile = ($_request ; $file) fun {
	$mime = fileMime: file.
	
	"ETag" ($setHeader propOf _request) $etag = mdFiveHash: fileContent: file.
	!ifElse (etag eq ($getHeader propOf _request): "if-none-match") {
		($respondCode propOf _request): 304.
	};{
		($startServing propOf _request): mime.
		($writeFile propOf _request): file.
	}.
}.

#{
	Serves a file (like $writeFile propOf _request), but with additional support for the "ETag" header and HTTP 304 (not modified) responses.
	The function can automatically write the file type to the request, which prevents other headers from being accepted later. So all headers should be set before calling this function.
	
	Usage example:
		_request httpWriteStaticFile "path/to/a/file".
	
	It is assumed that a file exists at the given path.
	The served file is not allowed to change later - because of that, the ETag is not content-specific.
	
	If you want to serve files which might change later, consider using "httpWriteGlobalFile".
}
$httpWriteStaticFile = ($_request ; $file) fun {
	"Cache-Control" ($setHeader propOf _request) "public, max-age=31536000, immutable".
	"ETag" ($setHeader propOf _request) $etag = fileName: file.
	!ifElse (etag eq ($getHeader propOf _request): "if-none-match") {
		($respondCode propOf _request): 304.
	};{
		($startServing propOf _request): fileMime: file.
		($writeFile propOf _request): file.
	}.
}