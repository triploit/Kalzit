/* @kalzit.for kmp_from_youtube_video_async
	Produces a KMP (Kalzit Media Protocol) representation of a YouTube Video, represented by a video ID.
	This includes the title information, which is parsed from the YouTube watch page.
	The media type (field "kind") is "video".
	
	Usage example:
		```kalzit
		{$kmpData = x} kmpFromYoutubeVideoAsync "videoId".
		```
	
	If you do not want to access the watch page or if you do not need the title, use "kmpFromYoutubeVideoNoTitles".
*/
$kmpFromYoutubeVideoAsync = $kalzitMediaProtocolFromYoutubeVideoAsync = ($callback ; $id ? YoutubeVideoId) fun {
	$thumbnailObject = ($default;$highres;$lowres) mingle "https://img.youtube.com/vi/" + id + "/" + ("mqdefault.jpg";"maxresdefault.jpg";"default.jpg").
	
	($oembed fun {
		callback:
			[$link;"https://www.youtube.com/watch?v=" + id];
			[$title;$title propOf oembed];
			["kind";"video"];
			["protocolVersion";"2.0.0"];
			[$thumbnail;[thumbnailObject]];
			["embedUrl";"https://www.youtube-nocookie.com/embed/" + id];
			["authorName"; $author_name propOf oembed]
	}) youtubeVideoOembedAsync id.
}.

/* @kalzit.for kmp_from_youtube_url_async
	Produces a KMP (Kalzit Media Protocol) representation by any YouTube URL.
	This includes the title information of the videos, which is parsed from the YouTube watch page.
	The media type (field "kind") is "collection", and the items are of type "video".
	
	Usage example:
		```kalzit
		!kmpFromYoutubeUrlAsync "https://www.youtube.com" -> {$startPageVideos = x}.
		```
	
	If you do not want to access the watch page or if you do not need the title, use "kmpFromYoutubeVideoNoTitles".
*/
$kmpFromYoutubeUrlAsync = $kalzitMediaProtocolFromYoutubeUrlAsync = ($callback ; $url ? String) fun {
	($ids fun {
		!multipleAsync (kmpFromYoutubeVideoAsync bindAsyncFunctionToEach ids) -> {
			callback: kmpFromObjectCollection: x
		}
	}) youtubeVideoIdsByUrlAsync url.
}.


`Implementations without titles`
/* @kalzit.for kmp_from_youtube_video_no_titles
	Produces a KMP (Kalzit Media Protocol) representation of a YouTube Video, represented by a video ID.
	This does not include the title information.
	The media type (field "kind") is "video".
	
	Usage example:
		```kalzit
		$kmpData = kmpFromYoutubeVideoNoTitles: "videoId".
		```
	
	If you do not want to access the titles, use "kmpFromYoutubeVideo".
*/
$kmpFromYoutubeVideoNoTitles = ($id ? YoutubeVideoId) fun {
	$thumbnailObject = ($default;$highres;$lowres) mingle "https://img.youtube.com/vi/" + id + "/" + ("mqdefault.jpg";"maxresdefault.jpg";"default.jpg").
	
	$baseObject =
		[$link;"https://www.youtube.com/watch?v=" + id];
		["kind";"video"];
		["protocolVersion";"2.0.0"];
		[$thumbnail;[thumbnailObject]];
		["embedUrl";"https://www.youtube-nocookie.com/embed/" + id].
}.

/* @kalzit.for kmp_from_youtube_url_no_titles
	Produces a KMP (Kalzit Media Protocol) representation by any YouTube URL.
	This does not include the title information.
	The media type (field "kind") is "collection", and the items are of type "video".
	
	Usage example:
		```kalzit
		$startPageVideos = kmpFromYoutubeUrlNoTitles: "https://www.youtube.com".
		```
	
	If you do not want to access the titles, use "kmpFromYoutubeUrl".
*/
$kmpFromYoutubeUrlNoTitles = (($url ? String) fun {
	$ids = youtubeVideoIdsByUrl: url.
	"{" + {"link":"} + url + {","kind":"collection","protocolVersion":"2.0.0","collection":[} + ("," strJoin objToJson each kmpFromYoutubeVideoNoTitles each ids) + {]} + "}".
}) cachedFun autoClearedCache: hoursToMillis: 2.
