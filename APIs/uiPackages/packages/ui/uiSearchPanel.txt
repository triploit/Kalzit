/* @kalzit.for uiSearchPanel

	`searchTermRef` is an optional (mutable) value which can be used to keep track of what is in the search field
*/
$uiSearchPanel = !fun ($searchResultFactory ; $searchTermRef ? Mutable) {
	$currentSearchResultsRef ? Var = @centerText uiContainer: strings: "pressGoHint".
	@(percentHeight 100) uiContainer: do:{
		$currentSearchTermRef = !ifElse (void eq searchTermRef) {
			`Construct a new mutable to store the search term`
			mutable: ""
		};{
			`Use the mutable given by the caller of this function`
			searchTermRef
		}.
		
		$action = !fun ($currentSearchTerm ? strTrim) {
			!if ("" eq currentSearchTerm) {
				currentSearchResultsRef = @centerText uiText: strings: "enterSearchTerm".
			}. !else {
				currentSearchResultsRef = @verticalScroller uiContainer: @(width "100% - 14px") @(percentHeight 100) @center uiContainer: searchResultFactory: ~currentSearchTermRef.
			}
		}.
		
		[uiCustomTextActionToolbar:
			[$buttonText; strings: "go"];
			[$action; action];
			[$inputReference; currentSearchTermRef]
		];
		[@belowToolbar uiShowVariable: currentSearchResultsRef]
	}.
}
