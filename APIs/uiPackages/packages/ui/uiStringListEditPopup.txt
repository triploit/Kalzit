#{
	The "configuration" parameter is an object that can have the following fields:
		- $toStringConverter (converts each item to a string)
		- $fromStringConverter (converts a string to a valid list item)
		- $onTap (when an item is selected - gets the selected index as parameter)
		- $onAddition (triggered when an item is added)
		- $onRemoval (triggered when an item is removed)
}
$uiCustomModifiableStringListEditor = ($list ? ModifiableList ; $configuration) fun {
	($setOnAddition propOf list): first: $onAddition propOf configuration.
	($setOnRemoval propOf list): $onRemoval propOf configuration.
	
	$stringFactory ? (default: {String: x}) = $toStringConverter propOf configuration.
	$itemFactory ? (default: {x}) = $fromStringConverter propOf configuration.
	$tapAction = $onTap propOf configuration.
	
	$nameViewsRef = reference: $nameViews.
	$updateNameViews = () fun {
		nameViewsRef = (($index) fun {
			$_item = index at rawList.
		
			@center uiContainer:
				(uiHorizontalPxSpace: 7);
				[@(cssClass: "width-tet-textfield") @(onTap: () fun {tapAction: index}) uiContainer: strFirstLine: stringFactory: _item];
				[uiHorizontalPxSpace: 7];
				[@(cssClass: "width-tet-button") $button = (() fun {
					($removeEquals propOf list): _item.
				}) uiButton strings: $removeButton];
				(uiHorizontalPxSpace: 7)
		}) each arrayIndexes: $rawList = getArray: list.
	}.
	do:updateNameViews.
	($setOnChange propOf list): updateNameViews.
	
	uiContainer: [
		($newAddition fun {
			($push propOf list): itemFactory: newAddition. do:updateNameViews
		}) uiTextActionToolbar strings: $addButton
	];[
		@verticalScroller @center @(height "100% - " + toolbarCmHeight + "cm") @(percentWidth 100) uiContainer: uiShowVariableAsList: nameViewsRef
	]
}.

$uiModifiableStringListEditor = ($list ? ModifiableList ; $_onAdditionAndOnRemoval) fun {
	$config =
		[$onAddition; first: _onAdditionAndOnRemoval];
		[$onRemoval; second: _onAdditionAndOnRemoval].
		
	list uiCustomModifiableStringListEditor config
}.

$uiStringListEditor = ($listRef ? Reference ; $_onAdditionAndOnRemoval) fun {
	$newAdditionRef = reference: $newAddition.
	$onAddition = first: _onAdditionAndOnRemoval.
	$onRemoval = second: _onAdditionAndOnRemoval.
	
	$modList ? ModifiableList = (resolveReference: listRef).
	modList uiModifiableStringListEditor {listRef onAddition x. listRef = getArray: modList};{listRef = getArray: modList. onRemoval: listRef}
}.

$uiStringListEditPopup = ($listRef ? Reference ; $_onAdditionAndOnRemoval) fun {
	(@(percentHeight 100) listRef uiStringListEditor _onAdditionAndOnRemoval) uiPopup {
		!closeSubApp
	}
}.
$uiModifiableStringListEditPopup = ($list ? ModifiableList ; $_onAdditionAndOnRemoval) fun {
	(@(percentHeight 100) list uiModifiableStringListEditor _onAdditionAndOnRemoval) uiPopup {
		!closeSubApp
	}
}.