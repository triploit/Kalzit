$showPopup = $uiShowPopup = !fun ($popup) {
	$active = !isPopupActive.
	!hideAllPopups.
	
	!ifNot active {
		@(cssClass "k-popup_animated_in") popup
	}.
	print: popup.
}.

`Only show the notification decision prompt when notifications are theoretically allowed. If they are not supported or forbidden, this variable is true - no dialog will appear`
$notificationPromptShownRef ? Var = !notificationForbidden.

$showNotificationPrompt = !fun ($message ? String) {
	!ifNot (~notificationPromptShownRef | !notificationAllowed) {
		$action = {uiHide: popup. do:callback}.
		
		$popupContent = @(cssClass: "k-vertical_scroller") @(percentHeight 100) uiContainer:
			[@centerText uiText: message];
			[@center uiContainer:
				[{
					uiHide: popup.
					!ifNot (~notificationPromptShownRef) {
						!notificationRequestPermission.
						notificationPromptShownRef = true.
					}.
				} uiButton strings: $allowNotifications];
				[{notificationPromptShownRef = true. uiHide: popup} uiButton strings: $dontAllow]
			].
		
		print: $popup = popupContent uiSmallPopup action.
	}
}.

$showConfirmMessageAsync = !fun ($onConfirm ; $message ? String) {
	$action = {uiHide: popup}.
	
	$popupContent = @(cssClass: "k-vertical_scroller") @(percentHeight 100) uiContainer:
		[@centerText uiText: message];
		[@center uiContainer:
			[{
				uiHide: popup.
				do: onConfirm
			} uiButton strings: $allow];
			[{uiHide: popup} uiButton strings: $dontAllow]
		].
	
	print: $popup = popupContent uiSmallPopup action.
}.

$showMessageAsync = !fun ($callback ; $message ? String) {
	$action = {uiHide: popup. do:callback}.

	$popupContent = @(cssClass: "k-vertical_scroller") @(percentHeight 100) uiContainer:
		[@centerText uiText: message];
		[@center action uiButton "OK"].
	
	print: $popup = popupContent uiSmallPopup action.
}.
$showMessage = !fun ($message ? String ; $notificationQuestion ? String) {
	!if notificationAllowed {
		notificationShowShortText: message.
	}. !else {
		{
			!ifNot (void eq notificationQuestion) {showNotificationPrompt: notificationQuestion}.
		} showMessageAsync message
	}
}
