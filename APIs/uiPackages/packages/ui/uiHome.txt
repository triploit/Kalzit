$uiHomeScreenIcon = ($iconGraphic ? UiElement ; $_applySpecificLook ? (default: {@(backgroundColor red) x})) fun {
	$ico = @rounded @(emWidth 5) @(emHeight 5) iconGraphic.
	_applySpecificLook: ico
}.
$uiHomeScreenAppButton = ($action ; $appInfo) fun {
	$appColor ? RgbColor = (default: 122): eval: $color propOf appInfo.
	$description = (default: "Untitled"): $title propOf appInfo.
	$iconPath = $icon propOf appInfo.
	
	$gradient = backgroundGradientTopToBottom: [appColor];[{max: 0;x} each appColor - 150].
	$border = borderColor: {max: 0;x} each appColor - 50.
	$graphic = !ifElse (not: void eq iconPath) {
		uiShowImageUrl: "/apps/" + ($id propOf appInfo) + "/" + iconPath.
	};{
		uiContainer: @(emFontSize 1,5) uiText: first: "" strSplit description.
	}.
	
	@(onTap action) @centerText uiContainer: (graphic uiHomeScreenIcon {@(foregroundColor white) @border @gradient x});uiText: description
}.
$uiSimpleHomeScreenApp = ($description ? String ; $action) fun {
	action uiHomeScreenAppButton
		[$title;description];
		[$color;[
			randomItem: "red";"green";"blue"
		]].
}.
$uiHomeScreen = ($_action ? (default: launchAppName)) fun {
	$view = uiColumns: void.
	$printToView = uiPrint: view.

	$appView = !fun ($_app) {
		!ifElse (developerMode) {
			printToView: (() fun {_action: $id propOf _app}) uiHomeScreenAppButton _app
		};{
			!if (not: $dev propOf _app) {
				printToView: (() fun {_action: $id propOf _app}) uiHomeScreenAppButton _app
			}
		}
	}.
	
	`Load the app data from memory - no server requests required`
	appView each standardApps.
	
	view.
}.