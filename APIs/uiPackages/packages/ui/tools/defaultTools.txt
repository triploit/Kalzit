/* @kalzit.for button_tool
	Produces a button-like tool to add to the mobile or desktop tool set.
	This tool was meant to use with something like "toolsAtLeft" or "toolAtRight".
	
	Usage example:
	```
	toolsAtLeft: [otherTools];[ !buttonTool "Title" -> {popupMessage: "Button action"} ].
	```
*/
$buttonTool = !fun ($action ; $title) {
	["mobile";{!action. void}];
	["desktop";{action uiButton title}];
	["title";title]
}.

$indexPickerTool = !fun ($titleAndRef ? (Tuple: String;Reference) ; $_values ? ModifiableList) {
	$title = first: titleAndRef.
	$ref = second: titleAndRef.
	
	["desktop";{@dynamicWidth ref uiLiveReferenceIndexPicker _values}];
	["mobile";{
		!uiTitledPopupViewAsList title ->
			{ref = x. !closeSubApp} uiIndexPickerForMobile strFirstLine each getArray: _values)
	}];
	["title";title]
}.

/* @kalzit.for custom_modifiable_list_edit_tool
	The "configuration" parameter is an object that can contain the following fields:
		- Every field available for "uiCustomModifiableStringListEditor"
		- $title (Defines the title of this tool, shown on narrow screens / windows)
*/
$customModifiableListEditTool = !fun ($configuration ; $_values ? ModifiableList) {
	$title = (default: "Custom list editor (needs a $title)"): $title propOf configuration.
	
	`Sub app registration - only used by the desktop button`
	$subAppName = "tool_" + urlEncodeParameter: title.
	
	["desktop";{
		(() fun {openSubApp: subAppName}) uiButton title
	}];
	["mobile";{
		@(percentHeight 100) _values uiCustomModifiableStringListEditor configuration
	}];
	["title"; title]
}.

$modifiableListEditTool = !fun ($titleAndCallbacks ; $_values ? ModifiableList) {
	$callbacks = !if (3 eq length: titleAndCallbacks) {
		(1 at titleAndCallbacks);
		2 at titleAndCallbacks
	}.
	$title = 0 at titleAndCallbacks.
	
	`Sub app registration - only used by the desktop button`
	$subAppName = "tool_" + urlEncodeParameter: title.
	
	["desktop";{
		(() fun {openSubApp: subAppName}) uiButton title
	}];
	["mobile";{
		@(percentHeight 100) _values uiModifiableStringListEditor callbacks
	}];
	["title";title]
}.

$listEditTool = !fun ($titleAndCallbacks ; $listRef ? Reference) {
	$callbacks = !if (3 eq length: titleAndCallbacks) {
		(1 at titleAndCallbacks);
		2 at titleAndCallbacks
	}.
	$title = 0 at titleAndCallbacks.
	
	`Sub app registration - only used by the desktop button`
	$subAppName = "tool_" + urlEncodeParameter: title.
	
	["desktop";{
		(() fun {openSubApp: subAppName}) uiButton title
	}];
	["mobile";{
		@(percentHeight 100) listRef uiStringListEditor callbacks
	}];
	["title";title]
}.
$popupTool = !fun ($title ; $popupContentGenerator) {
	`Sub app registration - only used by the desktop button`
	$subAppName = "tool_" + urlEncodeParameter: title.
	
	["desktop";{
		(() fun {openSubApp: subAppName}) uiButton title
	}];
	["mobile";{
		@(percentHeight 100) uiContainer: !popupContentGenerator
	}];
	["title";title]
}.
$separatorTool = !fun () {
	["desktop"; @(cmHeight toolbarCmHeight) @verticalCenter !uiHorizontalSeparator];
	["mobile"; !uiVerticalSeparator]
}.

$additionalActionsTool = !fun ($object ; $additionalActions) {
	["desktop"; object uiAdditionalActionsButton additionalActions];
	["mobile"; uiContainer: !each additionalActions -> $action fun {
		uiListItem:
			[$mainView; uiContainer: 0 at action];
			[$onTap; 1 at action];
			[$value; object]
	}]
}.
