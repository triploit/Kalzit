$generateDesktopToolsAtLeft = !fun ($_subTools) {
	{
		@left uiContainer: 
			`At the very left, add a little space`
			(uiHorizontalPxSpace: 7);
			`Add the tools registered by the app`
			5 uiAddHorizontalPxSpaces {do:$desktop propOf x} each _subTools
	}
}.
$generateDesktopToolAtLeft = !fun ($_tool) {
	generateDesktopToolsAtLeft: [_tool]
}.
$generateDesktopToolsAtRight = !fun ($_subTools) {
	{
		@dynamicWidth @right uiContainer: 
			`Add the tools registered by the app`
			5 uiAddHorizontalPxSpaces {do:$desktop propOf x} each _subTools
	}
}.
$generateDesktopToolAtRight = !fun ($_tool) {
	generateDesktopToolsAtRight: [_tool]
}.

$toolLayoutManager = !fun () {
	$desktopTools ? ModifiableList = void.
	$mobileTools ? ModifiableList = void.
	
	[$setToolsAtLeft; ($_subTools) fun {
		($push propOf desktopTools): generateDesktopToolsAtLeft: _subTools.
		($push propOf mobileTools): ($_subTool fun {
			($mobile propOf _subTool);($title propOf _subTool)
		}) each _subTools.
	}];
	[$setToolAtLeft; ($_subTool) fun {
		($push propOf desktopTools): generateDesktopToolAtLeft: _subTool.
		($push propOf mobileTools): [($mobile propOf _subTool);($title propOf _subTool)]
	}];
	[$setToolAtRight; ($_subTool) fun {
		($push propOf desktopTools): generateDesktopToolAtRight: _subTool.
		($push propOf mobileTools): [($mobile propOf _subTool);($title propOf _subTool)]
	}];
	[$getDesktopToolbarOverlay; () fun {
		uiDesktopToolbarOverlay: desktopTools
	}];
	[$getMobileToolbarOverlay; () fun {
		uiMobileToolbarOverlay: mobileTools
	}];
	[$applyMobileTools; () fun {
		applyMobileTools: getArray: mobileTools
	}]
}.

/* @kalzit.for toolsAtLeft

	Puts a list of tools at the left side of the global toolbar.
	This function must be called before the global toolbar is created (using uiGlobalToolbar).
	Also, if you want to add tools to the right of the toolbar, you have to add the ones on the left first.
	Otherwise, it might make the tools on the right not clickable.
*/
$toolsAtLeft = !fun ($_subTools) {
	`Desktop`
	registerDesktopTool: generateDesktopToolsAtLeft: _subTools.
	
	`Mobile`
	($_subTool fun {
		($mobile propOf _subTool) registerMobileTool ($title propOf _subTool)
	}) each _subTools.
}.

/* @kalzit.for toolAtLeft

	Puts a single tool at the left side of the global toolbar.
	This function must be called before the global toolbar is created (using uiGlobalToolbar).
	Also, if you want to add tools to the right of the toolbar, you have to add the ones on the left first.
	Otherwise, it might make the tools on the right not clickable.
*/
$toolAtLeft = !fun ($_tool) {
	toolsAtLeft: [_tool]
}.

/* @kalzit.for toolsAtRight

	Puts a list of tools at the right side of the global toolbar.
	This function must be called before the global toolbar is created (using uiGlobalToolbar).
	Also, if you want to add tools to the right of the toolbar, you have to add the ones on the left first.
	Otherwise, it might make the tools on the right not clickable.
*/
$toolsAtRight = !fun ($_subTools) {
	`Desktop`
	registerDesktopTool: generateDesktopToolsAtRight: _subTools.
	
	`Mobile`
	($_subTool fun {
		($mobile propOf _subTool) registerMobileTool ($title propOf _subTool)
	}) each _subTools.
}.

/* @kalzit.for toolAtRight

	Puts a single tool at the right side of the global toolbar.
	This function must be called before the global toolbar is created (using uiGlobalToolbar).
	Also, if you want to add tools to the right of the toolbar, you have to add the ones on the left first.
	Otherwise, it might make the tools on the right not clickable.
*/
$toolAtRight = !fun ($_tool) {
	toolsAtRight: [_tool]
}.