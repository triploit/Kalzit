$uiLoginForm = ($onLoginAttempt) fun {
	$nameField = $nameInput uiTextfieldVariableOnchange {}.
	$passwordInput = "". $passwordInputRef = reference: $passwordInput.
	$passwordField = {passwordInputRef = x} uiPasswordOnchange passwordInput.
	
	$action = !fun () {
		($response fun {
			!if ("0" eq first: response) {
				$calcitSession storageSaveString second: response.
				$calcitUserName storageSaveString nameInput.
				{onLoginAttempt: true} showMessageAsync strings: $loginSuccess.
			}. else {
				{onLoginAttempt: false} showMessageAsync second: response.
			}.
		}) serviceLoginUserAsync nameInput;passwordInput.
	}.
	
	$views =
		[uiText: strings: $username];
		[@(percentWidth: 100) nameField];
		[uiText: strings: $password];
		[@(percentWidth: 100) @(onEnter action) passwordField];
		[@(emHeight 1) uiContainer: void];
		[@center action uiButton strings: $login];
		[@(emHeight 2) uiContainer: void];
		[@centerText uiText: strings: $optionsSeparator];
		[@center {openSubApp: "register"} uiButton strings: $registerButton].
		
	uiContainer: views
}.

$uiRegisterForm = ($onLoginAttempt) fun {
	$nameField = $nameInput uiTextfieldVariableOnchange {}.
	$passwordInput = "". $passwordInputRef = reference: $passwordInput.
	$passwordField = {passwordInputRef = x} uiPasswordOnchange passwordInput.
	
	$views =
		[uiText: strings: $username];
		[@(percentWidth: 100) nameField];
		[uiText: strings: $password];
		[@(percentWidth: 100) passwordField];
		[@(emHeight 1) uiContainer: void];
		[@center {
			($response fun {
				!if ("0" eq first: response) {
					popupMessage: strings: $registrationSuccess.
					`The user most likely intends to be logged in, so we do that automatically`
					`TODO: this is duplicated code from "uiLoginForm" - unify that`
					!serviceLoginUserAsync (nameInput;passwordInput) -> ($response fun {
						!if ("0" eq first: response) {
							$calcitSession storageSaveString second: response.
							$calcitUserName storageSaveString nameInput.
							{onLoginAttempt: true} showMessageAsync strings: $loginSuccess.
						}. else {
							{onLoginAttempt: false} showMessageAsync second: response.
						}.
					}).
	        	}. else {
	        		popupMessage: second: response
	        	}.
			}) serviceRegisterUserAsync nameInput;passwordInput.
        } uiButton strings: $register].
		
	uiContainer: views
}
