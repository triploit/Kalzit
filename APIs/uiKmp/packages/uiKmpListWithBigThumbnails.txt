/* @kalzit.for ui_kmp_list_with_big_thumbnails
	Creates a list view from a KMP collection, with (relatively large) thumbnails and titles. Additionally, you can specify actions for tapping on a medium and add other interaction options.
	
	Parameters:
	* `interactionConfig` is an object with multiple fields (listed below).
	* `videoList` is a KMP object which either represents a collection ($kind is "collection") or a single medium (in this case, the medium will be shown as a one-item list).
	
	More details on the "interactionConfig" object:
	* `onTap` is a function which is called when the user selects a medium from the list. It gets the KMP object of that medium as its first parameter.
	* `additionalActions` is a list of pairs, where each pair contains a string (first item) and a function (second item). These pairs are used to construct an action picker ("three dot button"), where the string specifies the title of each action and the function specifies the action itself. When an action is selected by the user, the appropriate function is called with the KMP object of the medium the action is applied to (what "onTap" would get when the medium is selected)
	* `allowTitleOverflow` is a boolean value which specifies if the title can take up more than one line. The default value is true. If this is false, titles can be cut off.
	* `centerTitle` is a boolean value which specifies if the title is centered. Default value is true - if false, the title is aligned to the left.
	* `defaultThumbnail` (optional): defines a default icon which will be used if no fixed icon is specified by the KMP list item,
*/
$uiKmpListWithBigThumbnails = !fun ($interactionConfig ; $videoList) {
	$tapAction = $onTap propOf interactionConfig.
	$additionalActions = $additionalActions propOf interactionConfig.
	$allowTitleOverflow ? (default: true) = $allowTitleOverflow propOf interactionConfig.
	$centerTitle ? (default: true) = $centerTitle propOf interactionConfig.
	$defaultThumbnail = $defaultThumbnail propOf interactionConfig.
	
	($_media fun {
		$titleView = uiContainer:
			[@(percentWidth 30) uiShowImageUrl: (default: defaultThumbnail): kmpGetDefaultThumbnail: _media;[$keptArguments; [$keptArguments propOf videoList]];[$source; $source propOf videoList]];
			`This text can not be vertically centered because if we do that, it sometimes goes beyond the container and looks weird`
			[@(percentWidth 70) uiContainer: (default: "Untitled"): kmpGetTitle: _media].
			
		!ifNot (allowTitleOverflow) {
			@ellipsis titleView.
		}.
		!if (centerTitle) {
			@centerText titleView.
		}.
		
		`return` uiListItem:
			["onTap"; tapAction];
			["additionalActions"; [additionalActions]];
			["mainView"; titleView];
			["value"; [_media]]
		
	}) uiListView kmpCollectionToList: videoList.
}.

/* @kalzit.for ui_kmp_list
	A convenience function for producing a uiList of uiKmpListItem(s).
	The "config" object is used to configure each list item; it can be used exactly as documented for uiKmpListItem.
	The "listConfig" property of "config" can include all the configurations for "uiCustomListView", but you do not need to specify "itemViewFactory".
*/
$uiKmpList = !fun ($config ; $videoList) {
	(kmpCollectionToList: videoList) uiCustomListView
		[$itemViewFactory; (($_media ; $index) fun {
			_media uiKmpListItem config;[$index; [index]]
		})];
		($listConfig propOf config).
}.
