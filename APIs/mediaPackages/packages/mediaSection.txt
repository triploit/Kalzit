/* @kalzit.for replayFrom

	Causes a media controller to start playing from a specified timestamp.
	
	Usage example:
		myMedium replayFrom (0,5 * do: $getDuration propOf myMedium). `Starts playing from the middle`
*/
$replayFrom = ($_mediaController;$timestamp) fun {
	($setTime propOf _mediaController): timestamp. play _mediaController
}.

/* @kalzit.for replay

	Causes a media controller to start playing from the beginning (0 seconds in).
	
	Usage example:
		replay myMedium.
*/
$replay = $_mediaController fun {_mediaController replayFrom 0}.

/* @kalzit.for mediaSection

	Creates a media controller that acts on a section of a different one.
	Requires the media controller as the first parameter, and the start and end timestamps as the second (as a list with two numbers).
	
	For example, if the media controller "originalController" exists, and you want to cut of two seconds at the start and ten at the end, you could do this:
		$startTime = 2.
		$endTime = (do: $getDuration propOf originalController) - 10.
		$newController = originalController mediaSection startTime;endTime.
	
	Now, if you would use "newController" with the "replay" function, the original medium would start playing, but at 2 seconds in.
	Also, it would then stop playing 10 seconds before its original end.
*/
$mediaSection = ($_mediaController ; $_startAndEnd) fun {
	$onProgress = $onEnd = void.
	$onProgressRef = reference: $onProgress.
	$onEndRef = reference: $onEnd.
	$start = first: _startAndEnd.
	$end = second: _startAndEnd.
	
	$controller =
		[[$play];[{
			!if(start > do: $getProgress propOf _mediaController) {
				($setTime propOf _mediaController): start
			}.
			play _mediaController.
		}]];
		[[$pause];[$pause propOf _mediaController]];
		[[$getDuration];[end - start]];
		[[$setTime];[{
			($setTime propOf _mediaController): x + start.
		}]];
		[[$setVolume];[$setVolume propOf _mediaController]];
		[[$getVolume];[$getVolume propOf _mediaController]];
		[[$setOnEnd];[$callback fun {
			onEndRef = callback
		}]];
		[[$setOnProgress];[$callback fun {
			onProgressRef = callback
		}]];
		[[$getProgress];[() fun {
			(do: $getProgress propOf _mediaController) - start
		}]].
		
	($setOnProgress propOf _mediaController): ($timestamp ; $_controller) fun {
		!if (end < timestamp) {
			pause _mediaController.
			onEnd: controller
		}.
		(timestamp - start) onProgress controller
	}.
	
	controller
}.