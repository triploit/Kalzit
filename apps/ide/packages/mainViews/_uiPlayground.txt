$_ui_playground = !fun ($initialCode) {
    `To make stuff easy for now, just override "print" to always end up in our view`
    $playgroundView = uiContainer: void.
    $outputToFullView = uiPrint: playgroundView.
    
    `Here is the actual implementation of the playground; similar to the original`
    $makeCodePlayView = !fun ($initialCode ? (default: "")) {
        $resultRef ? Var = void..
        
        @(percentWidth 100) @(autocorrect false)
        $inputField = {
            resultRef = evalGlobally: x.
            
            `Seems like we want to make a new input field available`
            outputToFullView: !makeCodePlayView.
        } uiTextfieldOnaction initialCode.
        
        $view = uiContainer: inputField; uiShowVariable: resultRef.
        $outputToEvalView = {(uiPrint: view): x. x}.
        
        `return` view
    }.
    
    outputToFullView: makeCodePlayView: initialCode.
    
    `At the end, we need to return the playgroundView`
    playgroundView
}.
